
plugins {
    id "java"
    id "eclipse"
}

version = "${rootProject.fw_version}${-> getVersionSuffix()}"
group = "io.github.zekerzhayard"
archivesBaseName = rootProject.name + "Converter"

sourceCompatibility = targetCompatibility = 1.8
compileJava {
    sourceCompatibility = targetCompatibility = 1.8
}

configurations {
    provided {
        implementation.extendsFrom provided
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "com.google.code.gson:gson:2.8.7"
    provided rootProject
}

jar {
    manifest.attributes rootProject.jar.manifest.attributes
    manifest.attributes([
            "Main-Class": "io.github.zekerzhayard.forgewrapper.converter.Main"
    ])

    from configurations.provided.files.collect {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        zipTree(it)
    }
}

processResources {
    inputs.property "version", project.version
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        include "patches/net.minecraftforge.json"
        expand "version": project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        exclude "patches/net.minecraftforge.json"
    }
}
